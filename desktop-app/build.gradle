plugins {
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    manifest {
        attributes "Main-Class" : 'net.heretical_camelid.fhau.desktop_app.CommandLineInterface'
        attributes "Implementation-Version" : "$rootProject.ext.versionName"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from('..') {
        include "LICENSE"
        include "assets/copyright_and_warranty.txt"
        include "assets/warranty_disclaimer.txt"
        include "assets/dtcli_usage.txt"
        include "assets/dtcli_interactive_help.txt"
        include "assets/50-fhau.rules"
    }

    from('libs') {
        include "**/*.so"
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(':lib:jar')
}

base {
    archivesName = "desktopFHAUcli-$rootProject.ext.versionName"
}

dependencies {
    implementation project(':lib')
    implementation 'net.java.dev.jna:jna:5.17.0'
    implementation 'org.hid4java:hid4java:0.8.0'
    implementation 'com.github.hypfvieh:bluez-dbus:0.3.2'
    implementation 'com.github.hypfvieh:dbus-java-transport-native-unixsocket:5.1.1'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.slf4j:slf4j-simple:2.0.0'

    // TL 25/06/17
    // I want to experiment with a library called tinyb
    // from archived Github project
    // https://github.com/intel-iot-devkit/tinyb:master.

    // There used to be a gradle package for that at
    // in a repository on the bintray collection, but
    // that collection is no longer online
    // implementation 'intel-iot-devkit:tinyb:0.5.1'

    // So for the moment I have forked the code,
    // made some changes required to get it to
    // compile under Ubuntu 24.04/Java17, and
    // checked the compiled JAR into this package.
    implementation files('libs/tinyb-0.5.1.tl250617.jar')

    // The fork can be found here:
    // https://github.com/tim-littlefair/tl-tinyb
    // Note that the default branch on my fork is
    // called 250617-ubuntu2404_and_java17.
    // I plan to leave upstream master unmodified
    // as it is clear that the original owners
    // at Intel have no intention of accepting
    // PRs.
}
